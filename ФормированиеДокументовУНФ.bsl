
//////////////////////////////////////////////////////////////////////////////////
//
//	Модуль Формирования документов для УНФ
//	Версия: 1.0.0.1 от 04.09.2025
//	
//	Автор и разработчик: Раковиц Владимир
//	e-mail: rakovitc@gmail.com
//	https://github.com/vogrosty/
//	
//////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует новую структуру основных реквизитов документа с заполненными данными по-умолчанию
// 
Функция СтруктураДокумент(ТипДокумента = "") Экспорт 
	
	СтруктураДокумент = Новый Структура;
	СтруктураДокумент.Вставить("Дата", ТекущаяДатаСеанса());
	СтруктураДокумент.Вставить("Автор", ПользователиКлиентСервер.ТекущийПользователь());
	СтруктураДокумент.Вставить("Комментарий", ""); 
	СтруктураДокумент.Вставить("Организация", Справочники.Организации.ОрганизацияПоУмолчанию());
	
	// Специфичное для Перемещений запасов (с операцией Перемещение)
	Если ТипДокумента = "ПеремещениеЗапасов" Тогда
		
		СтруктураДокумент.Вставить("СтруктурнаяЕдиница", Справочники.СтруктурныеЕдиницы.ОсновнойСклад());
		СтруктураДокумент.Вставить("ВидОперации", Перечисления.ВидыОперацийПеремещениеЗапасов.Перемещение);
		СтруктураДокумент.Вставить("СтруктурнаяЕдиницаПолучатель", Справочники.СтруктурныеЕдиницы.ОсновнойСклад());
		СтруктураДокумент.Вставить("ХозяйственнаяОперация", Справочники.ХозяйственныеОперации.Перемещение);
		СтруктураДокумент.Вставить("ПоложениеЗаказаПокупателя", Перечисления.ПоложениеРеквизитаНаФорме.ВШапке);
		СтруктураДокумент.Вставить("ТипДокумента", "ПеремещениеЗапасов");
		СтруктураДокумент.Вставить("Запасы", Новый Массив);
		
	// Специфичное для Перемещений запасов (с операцией Списание на расходы)
	ИначеЕсли ТипДокумента = "СписаниеНаРасходы" Тогда
		
		СтруктураДокумент.Вставить("ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
		СтруктураДокумент.Вставить("СтруктурнаяЕдиница", Справочники.СтруктурныеЕдиницы.ОсновнойСклад());
		СтруктураДокумент.Вставить("ВидОперации", Перечисления.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы);
		СтруктураДокумент.Вставить("СтруктурнаяЕдиницаПолучатель", Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение());
		СтруктураДокумент.Вставить("ХозяйственнаяОперация", Справочники.ХозяйственныеОперации.СписаниеНаРасходы);
		СтруктураДокумент.Вставить("СчетЗатрат", ПланыСчетов.Управленческий.КоммерческиеРасходы);
		СтруктураДокумент.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ОсновноеНаправление);
		СтруктураДокумент.Вставить("ПоложениеЗаказаПокупателя", Перечисления.ПоложениеРеквизитаНаФорме.ВШапке);
		СтруктураДокумент.Вставить("ТипДокумента", "ПеремещениеЗапасов");
		СтруктураДокумент.Вставить("Запасы", Новый Массив);
		
	ИначеЕсли ТипДокумента = "РезервированиеЗапасов" Тогда 
		
		СтруктураДокумент.Вставить("ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
		СтруктураДокумент.Вставить("ХозяйственнаяОперация", Справочники.ХозяйственныеОперации.РезервированиеЗапасов);
		СтруктураДокумент.Вставить("ТипДокумента", "РезервированиеЗапасов");
		СтруктураДокумент.Вставить("Запасы", Новый Массив);
		
		
	КонецЕсли;
	
	Возврат СтруктураДокумент;
	
КонецФункции

// Формирует новый документ на основании структуры документа
//
// Параметры:
//  СтруктураДокумент - Структура - Заполненная структура (см. СтруктураДокумент())
//
//  РежимЗаписи - РежимЗаписиДокумента - По умолчанию Проведение 
//
Функция СформироватьДокументИзСтруктуры(СтруктураДокумент, РежимЗаписи = Неопределено) Экспорт  

	ТипДокумента = "";
	
	Если Не СтруктураДокумент.Свойство("ТипДокумента", ТипДокумента) Тогда
		ОбщегоНазначения.СообщитьПользователю("При создании документа не указан тип!");
		Возврат Ложь;		
	КонецЕсли;
	
	Если РежимЗаписи = Неопределено Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;	
	КонецЕсли;
	
	НовыйДокумент = Документы[ТипДокумента].СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(НовыйДокумент, СтруктураДокумент);
	
	Для Каждого СтруктураЗапас Из СтруктураДокумент.Запасы Цикл
		СтрокаЗапасы = НовыйДокумент.Запасы.Добавить();	
		ЗаполнитьЗначенияСвойств(СтрокаЗапасы, СтруктураЗапас);
		Если Не ЗначениеЗаполнено(СтрокаЗапасы.ЕдиницаИзмерения) Тогда
			СтрокаЗапасы.ЕдиницаИзмерения = СтрокаЗапасы.Номенклатура.ЕдиницаИзмерения;
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		
		НовыйДокумент.Записать(РежимЗаписи);
		СтруктураДокумент.Вставить("Ссылка", НовыйДокумент.Ссылка);
		Возврат Истина;
		
	Исключение
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти  
